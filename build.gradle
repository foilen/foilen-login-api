buildscript {
  repositories {
    mavenLocal()
    jcenter()
  }
  dependencies {
    classpath 'io.spring.gradle:dependency-management-plugin:1.0.3.RELEASE'
  }
}

plugins {
  id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group = 'com.foilen'
version = System.getenv('VERSION')?:'master-SNAPSHOT'

repositories {
  mavenLocal()
  jcenter()
  mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencyManagement {
  imports {
    mavenBom 'io.spring.platform:platform-bom:Brussels-SR5'
  }
}

eclipse {
  classpath {
    downloadSources = true
    downloadJavadoc = true
  }
}

dependencies {

  // Logging
  compile "org.slf4j:slf4j-api"

  // Spring
  compile "commons-logging:commons-logging"
  compile "org.apache.httpcomponents:httpclient"
  compile "org.springframework:spring-web"
  compile "org.springframework:spring-context-support"
  compile "org.springframework.security:spring-security-config"
  compile "org.springframework.security:spring-security-web"

  // JSON
  compile "com.fasterxml.jackson.core:jackson-databind"

  // Security
  compile "com.madgag.spongycastle:prov:$spongycastleVersion"
  compile "com.madgag.spongycastle:pkix:$spongycastleVersion"
  compile "com.madgag.spongycastle:pg:$spongycastleVersion"

  // Misc
  compile 'com.google.guava:guava'
  compile "com.foilen:jl-incubator:$smalltoolsVersion"
  compile "com.foilen:jl-smalltools:$smalltoolsVersion"
  compile "javax.servlet:javax.servlet-api"
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')

  publish = true

  pkg {
    repo = 'maven'
    userOrg = 'foilen'
    name = 'com.foilen:foilen-login-api'
    licenses = ['MIT']
    vcsUrl = 'https://github.com/foilen/foilen-login-api'
  }

  publications = ['MyPublication']
}

publishing {
  publications {
    MyPublication(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      groupId 'com.foilen'
      artifactId 'foilen-login-api'
    }
  }
}

task wrapper(type: Wrapper) { gradleVersion = '4.1' }
